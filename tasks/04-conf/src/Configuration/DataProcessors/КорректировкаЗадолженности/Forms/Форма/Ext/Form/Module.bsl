
&НаСервере
Процедура СкорректироватьНаСервере()
	
	ДатаКорректировки = КонецДня(Объект.Дата);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Покупатели);
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УправленческийОстатки.Субконто1 КАК Контрагент,
		|	УправленческийОстатки.Субконто2 КАК Договор,
		|	УправленческийОстатки.СуммаВалОстаток КАК СуммаВалОстаток,
		|	УправленческийОстатки.СуммаОстаток КАК СуммаОстаток,
		|	УправленческийОстатки.Субконто2.Валюта КАК Валюта
		|ПОМЕСТИТЬ втЗадолженности
		|ИЗ
		|	РегистрБухгалтерии.Управленческий.Остатки(&Период, Счет = &Счет, &ВидыСубконто, ) КАК УправленческийОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втЗадолженности.Контрагент КАК Контрагент,
		|	втЗадолженности.Договор КАК Договор,
		|	втЗадолженности.СуммаВалОстаток КАК СуммаВалОстаток,
		|	втЗадолженности.Валюта КАК Валюта,
		|	втЗадолженности.СуммаВалОстаток * ВЫБОР
		|		КОГДА КурсыВалютСрезПоследних.Валюта <> ЗНАЧЕНИЕ(Справочник.Валюты.РоссийскийРубль)
		|			ТОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0)
		|		ИНАЧЕ 1
		|	КОНЕЦ - втЗадолженности.СуммаОстаток КАК КурсоваяРазница
		|ИЗ
		|	втЗадолженности КАК втЗадолженности
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
		|				&Период,
		|				Валюта В
		|					(ВЫБРАТЬ
		|						втЗадолженности.Валюта КАК Валюта
		|					ИЗ
		|						втЗадолженности КАК втЗадолженности)) КАК КурсыВалютСрезПоследних
		|		ПО втЗадолженности.Валюта = КурсыВалютСрезПоследних.Валюта
		|ГДЕ
		|	втЗадолженности.СуммаВалОстаток * ВЫБОР
		|			КОГДА КурсыВалютСрезПоследних.Валюта <> ЗНАЧЕНИЕ(Справочник.Валюты.РоссийскийРубль)
		|				ТОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0)
		|			ИНАЧЕ 1
		|		КОНЕЦ - втЗадолженности.СуммаОстаток <> 0";
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Покупатели);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Договоры);
	
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	Запрос.УстановитьПараметр("Период", ДатаКорректировки + 1);
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.Управленческий.Покупатели);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Нет данных для формирования движений!";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
	
		НовыйДокумент = Документы.Операция.СоздатьДокумент();
		НовыйДокумент.Дата = ДатаКорректировки;
		НовыйДокумент.Записать();
		
		НаборЗаписей = РегистрыБухгалтерии.Управленческий.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(НовыйДокумент.Ссылка);
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.КурсоваяРазница > 0 Тогда
				Движение = НаборЗаписей.Добавить();
				Движение.Период = ДатаКорректировки;
				
				Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
				Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Покупатели] = Выборка.Контрагент;
				Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Договоры] = Выборка.Договор;
				
				Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
				
				Движение.Сумма = Выборка.КурсоваяРазница;
			Иначе
				Движение = НаборЗаписей.Добавить();
				Движение.Период = ДатаКорректировки;
				
				Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
				
				Движение.СчетКт = ПланыСчетов.Управленческий.Покупатели;
				Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Покупатели] = Выборка.Контрагент;
				Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Договоры] = Выборка.Договор;
				
				Движение.Сумма = -Выборка.КурсоваяРазница;
			КонецЕсли;
			
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Корректировка прошла успешно!";
		Сообщение.Сообщить();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Сообщение.Сообщить();
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура Скорректировать(Команда)
	Если ПроверитьЗаполнение() Тогда
		СкорректироватьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Объект.Дата = НачалоДня(ТекущаяДата());
КонецПроцедуры
