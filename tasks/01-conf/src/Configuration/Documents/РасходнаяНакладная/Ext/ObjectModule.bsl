
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	//Проверка заполнения резвизита КурсДоллара
	Если КурсДоллара = 0 Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указан курс доллара";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	//Получение актуального метода списания себестоимости на дату документа, FIFO или LIFO
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетнаяПолитикаСрезПоследних.МетодСписанияСебестоимости КАК МетодСписанияСебестоимости
		|ИЗ
		|	РегистрСведений.УчетнаяПолитика.СрезПоследних(&Период, ) КАК УчетнаяПолитикаСрезПоследних";
	
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	//Проверяем задан ли метод списания себестоимости в регистре сведений УчетнаяПолитика, если запрос пустой, 
	//следовательно в регистре нет заданного значения
	Если РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указан метод списания себестоимости";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий(); 
	
	//Создание переменной МетодСписанияСебестоимости и присвоение ей значения из запроса к регистру сведений
	МетодСписанияСебестоимости = ВыборкаДетальныеЗаписи.МетодСписанияСебестоимости;
	
	//Взведение флагов для записи движений в регистры накопления ОстаткиНоменклатуры и Продажи
	Движения.ОстаткиНоменклатуры.Записать();
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	
	//Описание управляемой блокировки регистра накопления ОстаткиНоменклатуры
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	//Запрос к табличной части документа РасходнаяНакладная, 
	//создание временной таблицы и левое соединение с регистром накопления ОстаткиНоменклатуры
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТЧ_Товары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧ_Товары.Номенклатура КАК Номенклатура,
		|	втТЧ_Товары.Количество КАК Количество,
		|	втТЧ_Товары.Сумма КАК Сумма,
		|	втТЧ_Товары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СебестоимостьВРубляхОстаток, 0) КАК СебестоимостьВРубляхОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СебестоимостьВДолларахОстаток, 0) КАК СебестоимостьВДолларахОстаток
		|ИЗ
		|	втТЧ_Товары КАК втТЧ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						втТЧ_Товары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						втТЧ_Товары КАК втТЧ_Товары)) КАК ОстаткиНоменклатурыОстатки
		|		ПО втТЧ_Товары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОстаткиНоменклатурыОстатки.Партия.МоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	//Проверка значения переменной МетодСписанияСебестоимости,
	//в зависимости от того, какой метод списания на дату актуален, меняется текст внутри запроса,
	//меняется порядок упорядочивания партий товаров
	Если МетодСписанияСебестоимости = Перечисления.УчетнаяПолитика.ФИФО Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Цикл по итоговым записям для контроля остататков
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		СебестоимостьОбщаяВРублях = 0;
		СебестоимостьОбщаяВДолларах = 0;
		
			Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2 единиц",
											ВыборкаНоменклатура.НоменклатураПредставление,
											ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток);
				Сообщение.Сообщить();
			КонецЕсли;
			
			//Если отказ, переход к следующей итерации, для вывода сообщения о нехватке товаров по всем номенклатурным позициям
			Если Отказ Тогда
				Продолжить;
			КонецЕсли;
			
			КоличествоСписать = ВыборкаНоменклатура.Количество;
			ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
			
			//Внутренний цикл по партиям, расчет себестоимости в двух валютах
			Пока ВыборкаДетальныеЗаписи.Следующий() И КоличествоСписать > 0 Цикл 
				Количество = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
				
				Если Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
					СебестоимостьВРублях = ВыборкаДетальныеЗаписи.СебестоимостьВРубляхОстаток;	
					СебестоимостьВДолларах = ВыборкаДетальныеЗаписи.СебестоимостьВДолларахОстаток
				Иначе
					СебестоимостьВРублях = ВыборкаДетальныеЗаписи.СебестоимостьВРубляхОстаток * Количество / ВыборкаДетальныеЗаписи.КоличествоОстаток;
					СебестоимостьВДолларах = ВыборкаДетальныеЗаписи.СебестоимостьВДолларахОстаток * Количество / ВыборкаДетальныеЗаписи.КоличествоОстаток;
				КонецЕсли; 
				
				//Запись расходных движений в регистр ОстаткиНоменклатуры
				Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
				Движение.Период = Дата;
				Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
				Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
				Движение.Количество = Количество;
				Движение.СебестоимостьВРублях = СебестоимостьВРублях;
				Движение.СебестоимостьВДолларах = СебестоимостьВДолларах;
				
				СебестоимостьОбщаяВРублях = СебестоимостьОбщаяВРублях + СебестоимостьВРублях;
				СебестоимостьОбщаяВДолларах = СебестоимостьОбщаяВДолларах + СебестоимостьВДолларах;

				КоличествоСписать = КоличествоСписать - Количество;
				
			КонецЦикла;
			
		//Запись оборотных движений в регистр накопления Продажи,
		//для учёта и дальнейшего анализа продаж товаров
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
		Движение.Количество = ВыборкаНоменклатура.Количество;
		Движение.СебестоимостьВРублях = СебестоимостьОбщаяВРублях;
		Движение.СебестоимостьВДолларах = СебестоимостьОбщаяВДолларах;
		Движение.СуммаВРублях = ВыборкаНоменклатура.Сумма;
		Движение.СуммаВДолларах = ВыборкаНоменклатура.Сумма / КурсДоллара;	
	КонецЦикла;
КонецПроцедуры






