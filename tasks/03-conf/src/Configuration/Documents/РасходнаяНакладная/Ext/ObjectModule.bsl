
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// =====ОПРЕДЕЛЕНИЕ МЕТОДА СПИСАНИЯ =====
	// Получаем метод списания (ФИФО/ЛИФО) из учетной политики на дату документа
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетнаяПолитикаСрезПоследних.МетодСписанияСебестоимости КАК МетодСписанияСебестоимости
		|ИЗ
		|	РегистрСведений.УчетнаяПолитика.СрезПоследних(&Дата, ) КАК УчетнаяПолитикаСрезПоследних";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнен метод списания себестоимости";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	МетодСписанияСебестоимости = Выборка.МетодСписанияСебестоимости;
	
	// =====СПИСАНИЕ ПО НОВОЙ МЕТОДИКЕ И СОЗДАНИЕ ВРЕМЕННОЙ ТАБЛИЦЫ =====
	//Запрос к табличной части документа.
	//Создаем временную таблицу с товарами из документа для многократного использования.
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Количество КАК Количество
		|ИЗ
		|	втТЧТовары КАК втТЧТовары";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	//Цикл по выборке из запроса, списываем товары.
	Пока Выборка.Следующий() Цикл
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Склад = Склад;
			Движение.Количество = Выборка.Количество;
	КонецЦикла;
	
	//Взводим флаг, отключаем разделение итогов и записываем движения.
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	// ===== КОНТРОЛЬ ОСТАТКОВ =====
	// Проверяем, не образовались ли отрицательные остатки после списания.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ОстаткиНоменклатурыОстатки.Склад.Представление КАК СкладПредставление
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&Граница,
		|			Склад = &Склад
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						Т.Номенклатура
		|					ИЗ
		|						втТЧТовары КАК Т)) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно номенклатуры %1 на складе %2 в количестве %3",
										Выборка.НоменклатураПредставление,
										Выборка.СкладПредставление,
										-Выборка.КоличествоОстаток);
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ===== СПИСАНИЕ СЕБЕСТОИМОСТИ ПО ФИФО/ЛИФО =====
	// Подготавливаем движения по себестоимости
	Движения.ОстаткиСебестоимости.Записать();
	Движения.ОстаткиСебестоимости.Записывать = Истина;
	
	// Запрос для получения партий с учетом выбранного метода
	// Используем временную таблицу, созданную ранее, соединяемся с регистром ОстаткиСебестоимости.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Количество КАК Количество,
		|	ОстаткиСебестоимостиОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ОстаткиСебестоимостиОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиСебестоимостиОстатки.СебестоимостьОстаток, 0) КАК СебестоимостьОстаток
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиСебестоимости.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						втТЧТовары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						втТЧТовары КАК втТЧТовары)) КАК ОстаткиСебестоимостиОстатки
		|		ПО втТЧТовары.Номенклатура = ОстаткиСебестоимостиОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОстаткиСебестоимостиОстатки.Партия.МоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	// Для ФИФО меняем порядок сортировки на возрастание.
	Если МетодСписанияСебестоимости = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УБЫВ", "ВОЗР");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// ===== РАСПРЕДЕЛЕНИЕ СПИСАНИЯ ПО ПАРТИЯМ =====
	// Списываем пропорционально остаткам в партиях.
	// Контроль остатков, на этом этапе будет излишним, так как мы ранее уже его провели,
	// если возникнут отрицательные остатки, документ не будет проведен и дальнейшие действия выполнены не будут.
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		КоличествоСписать = ВыборкаНоменклатура.Количество;
		
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() И КоличествоСписать > 0 Цикл
			
			КоличествоТемп = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
				
			Если КоличествоТемп = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
				Себестоимость = ВыборкаДетальныеЗаписи.СебестоимостьОстаток;
			Иначе
				Себестоимость = ВыборкаДетальныеЗаписи.СебестоимостьОстаток * КоличествоТемп / ВыборкаДетальныеЗаписи.КоличествоОстаток;
			КонецЕсли;
			
			 // Формируем движение расхода по себестоимости
			Движение = Движения.ОстаткиСебестоимости.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
			Движение.Количество = КоличествоТемп;
			Движение.Себестоимость = Себестоимость;
				
			КоличествоСписать = КоличествоСписать - КоличествоТемп;	
		КонецЦикла;
	КонецЦикла;
	
	// ===== БУХГАЛТЕРСКИЙ УЧЁТ =====
	// Взводим флаг для записи движений в регистр бухгалтерии.
	// Получаем курс валюты для пересчета суммы.
	// Функция получения курса валюты описана в общем модуле.
	Движения.Управленческий.Записывать = Истина;
	КурсВалюты = ОбщегоНазначения.КурсВалютыДоговора(Договор, Дата);
	
	Если КурсВалюты = 0 Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не задан курс валюты договора.";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	//Формируем движение по регистру бухгалтерии.
	// Проводка: Дт Покупатели - Кт ПрибылиУбытки
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Покупатели] = Контрагент;
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Договоры] = Договор;
	Движение.СуммаВалДт = СписокНоменклатуры.Итог("Сумма");
	Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
	Движение.Сумма = Движение.СуммаВалДт * КурсВалюты;
	
КонецПроцедуры
