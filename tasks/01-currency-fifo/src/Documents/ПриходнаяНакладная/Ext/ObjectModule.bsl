
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	//Проверка заполнения реквизита КурсДоллара
	Если КурсДоллара = 0 Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указан курс доллара";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	//Флаг записи движений в регистр накопления ОстаткиНоменклатуры
	Движения.ОстаткиНоменклатуры.Записывать = Истина;  

	//Запрос к табличной части СписокНоменклатуры документа ПриходнаяНакладная
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	//Цикл по выборке из запроса и запись приходных движений в регистр накопления ОстаткиНоменклатуры
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Партия = Ссылка;
		Движение.Количество = Выборка.Количество;
		Движение.СебестоимостьВРублях = Выборка.Сумма;
		Движение.СебестоимостьВДолларах = Выборка.Сумма / КурсДоллара;
	КонецЦикла
	
КонецПроцедуры

