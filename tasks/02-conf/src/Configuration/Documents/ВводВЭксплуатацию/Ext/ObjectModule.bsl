
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Регистрируем движения: списание с остатков и ввод в эксплуатацию
	Движения.ОстаткиНоменклатуры.Записать();
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	Движения.ОборудованиеВЭксплуатации.Записать();
	Движения.ОборудованиеВЭксплуатации.Записывать = Истина; 
	
	НачалоДняДокумента = НачалоДня(Дата);
	
	// ===== КРИТИЧЕСКИЙ МОМЕНТ: БЛОКИРОВКА ДАННЫХ =====
	// Для предотвращения двойного ввода одного оборудования разными пользователями
	// блокируем остатки по сроку годности (от начала дня документа)
	// Это гарантирует, что два документа не выберут одни и те же остатки
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	// Фильтруем только оборудование с неистекшим сроком годности
    	// Диапазон от начала дня документа до бесконечности
	ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(НачалоДняДокумента));
	
	// Блокируем конкретную номенклатуру из документа
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	// ===== ЗАПРОС ДЛЯ ПРИОРИТЕТНОГО СПИСАНИЯ =====
    // Получаем остатки, упорядоченные по сроку годности (FIFO по сроку)
    // Сначала списываем оборудование с наименьшим оставшимся сроком годности
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ВводВЭксплуатациюСписокНоменклатуры.Количество) КАК Количество,
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура.СрокЭксплуатацииДней КАК НоменклатураСрокЭксплуатацииДней
		|ПОМЕСТИТЬ втТЧОборудование
		|ИЗ
		|	Документ.ВводВЭксплуатацию.СписокНоменклатуры КАК ВводВЭксплуатациюСписокНоменклатуры
		|ГДЕ
		|	ВводВЭксплуатациюСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура,
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура.СрокЭксплуатацииДней
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧОборудование.Номенклатура КАК Номенклатура,
		|	втТЧОборудование.Количество КАК Количество,
		|	ОстаткиНоменклатурыОстатки.СрокГодности КАК СрокГодности,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СебестоимостьОстаток, 0) КАК СебестоимостьОстаток,
		|	втТЧОборудование.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ДОБАВИТЬКДАТЕ(&НачалоДня, ДЕНЬ, втТЧОборудование.НоменклатураСрокЭксплуатацииДней) КАК СрокЭксплуатации
		|ИЗ
		|	втТЧОборудование КАК втТЧОборудование
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|						(ВЫБРАТЬ
		|							втТЧОборудование.Номенклатура КАК Номенклатура
		|						ИЗ
		|							втТЧОборудование КАК втТЧОборудование)
		|					И СрокГодности >= &НачалоДня) КАК ОстаткиНоменклатурыОстатки
		|		ПО втТЧОборудование.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	СрокГодности
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("НачалоДня", НачалоДняДокумента);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//По итоговым записям проводим контроль остатков и выводим сообщения в случае нехватки номенклатуры.
	Пока ВыборкаНоменклатура.Следующий() Цикл 
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Оборудования %1, в приходной накладной было меньше, чем вы пытаетесь ввести в эксплуатацию, на %2 единиц",
										ВыборкаНоменклатура.НоменклатураПредставление,
										ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток);
			Сообщение.Сообщить();
		КонецЕсли;
		
		//Если отказ тогда идём в следующую итерацию цикла
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоСписать = ВыборкаНоменклатура.Количество;
		Выборка = ВыборкаНоменклатура.Выбрать();
		
		//Далее выборка по детальным записям, в ней рассчитываем себестоимость по условию последнего списания и делаем расходные движения по
		//регистру ОстаткиНоменклатуры и приходные движения по регистру ОборудованиеВЭкспулатацию.
		Пока Выборка.Следующий() И КоличествоСписать > 0 Цикл
			Количество = Мин(КоличествоСписать, Выборка.КоличествоОстаток);
			Если Количество = Выборка.КоличествоОстаток Тогда
				Себестоимость = Выборка.СебестоимостьОстаток;
			Иначе
				Себестоимость = Выборка.СебестоимостьОстаток *  Количество / Выборка.КоличествоОстаток;
			КонецЕсли;
			
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.СрокГодности = Выборка.СрокГодности;
			Движение.Количество = Количество;
			Движение.Себестоимость = Себестоимость;
						
			Движение = Движения.ОборудованиеВЭксплуатации.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.СрокГодности = Выборка.СрокГодности;
			Движение.СрокЭксплуатации = Выборка.СрокЭксплуатации;
			Движение.Количество = Количество;
			Движение.Себестоимость = Себестоимость;  
			
			КоличествоСписать = КоличествоСписать - Количество;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры
