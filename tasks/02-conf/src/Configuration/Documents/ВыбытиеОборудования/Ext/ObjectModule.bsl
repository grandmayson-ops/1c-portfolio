
Процедура ОбработкаПроведения(Отказ, РежимПроведения) 
	
	// Регистрируем движения: списание с остатков и ввод в эксплуатацию
	Движения.ОстаткиНоменклатуры.Записать();
	Движения.ОстаткиНоменклатуры.Записывать = Истина; 
	
	Движения.ОборудованиеВЭксплуатации.Записать();
	Движения.ОборудованиеВЭксплуатации.Записывать = Истина;
	
	НачалоДняДокумента = НачалоДня(Дата);
	
	// ===== КРИТИЧЕСКИЙ МОМЕНТ: БЛОКИРОВКА ДАННЫХ =====
	//По остаткам номенклатуры блокируем по срокам годности, диапазон ДО начала дня документа, так как
	//мы смотрим просроченную номенклатуру, по оборудованию в эксплуатацию блокируются как сроки годности, так и
	//сроки эксплуатации, диапазон так же ДО начала дня документа.
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(,НачалоДняДокумента));
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОборудованиеВЭксплуатации");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(,НачалоДняДокумента));
	ЭлементБлокировки.УстановитьЗначение("СрокЭксплуатации", Новый Диапазон(,НачалоДняДокумента));
	Блокировка.Заблокировать();
	
	// ===== ЗАПРОС К ДВУМ РЕГИСТРАМ НАКОПЛЕНИЯ =====
   	//Получаем остатки по двум регистрам накопления, с истёкшими сроками годности и
	//сроками экспулатации.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.СрокГодности КАК СрокГодности,
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Количество,
		|	ОстаткиНоменклатурыОстатки.СебестоимостьОстаток КАК Себестоимость
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(&МоментВремени, СрокГодности < &НачалоДня) КАК ОстаткиНоменклатурыОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОборудованиеВЭксплуатацииОстатки.Номенклатура КАК Номенклатура,
		|	ОборудованиеВЭксплуатацииОстатки.СрокГодности КАК СрокГодности,
		|	ОборудованиеВЭксплуатацииОстатки.СрокЭксплуатации КАК СрокЭксплуатации,
		|	ОборудованиеВЭксплуатацииОстатки.КоличествоОстаток КАК Количество,
		|	ОборудованиеВЭксплуатацииОстатки.СебестоимостьОстаток КАК Себестоимость
		|ИЗ
		|	РегистрНакопления.ОборудованиеВЭксплуатации.Остатки(
		|			&МоментВремени,
		|			СрокГодности < &НачалоДня
		|				ИЛИ СрокЭксплуатации < &НачалоДня) КАК ОборудованиеВЭксплуатацииОстатки";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("НачалоДня", НачалоДняДокумента);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатыЗапросов[0].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.Период = Дата;
	КонецЦикла;  
	
	Выборка = РезультатыЗапросов[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОборудованиеВЭксплуатации.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.Период = Дата;
	КонецЦикла;
	
КонецПроцедуры
