
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	//Запрос к табличной части документа ПриходнаяНакладная
	//В данном запросе примечательно то, что срок годности мы вытаскиваем из табличной части документа с типом число.
	//Чтобы сделать запись в регистр накопления ОстаткиНоменклатуры, нам необходимо преобразовать это число в дату(срок годности).
	//Для этого используется метод ДОБАВИТЬКДАТЕ(), в котором мы указываем к какой дате прибавляем(в данном случае это дата документа, 
	//Тип(ДЕНЬ, МЕСЯЦ и др.) и количество(в данном случае дней) и получаем в конечном итоге срок годности опредленной номенклатуры.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Себестоимость,
		|	ДОБАВИТЬКДАТЕ(&Период, ДЕНЬ, ПриходнаяНакладнаяСписокНоменклатуры.СрокГодностиДней) КАК СрокГодности
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	ДОБАВИТЬКДАТЕ(&Период, ДЕНЬ, ПриходнаяНакладнаяСписокНоменклатуры.СрокГодностиДней)";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	//Цикл по выборке результата запроса, добавляем приходные движения, заполняем поля из выборки, период это дата документа.
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.СрокГодности = Выборка.СрокГодности;
		Движение.Количество = Выборка.Количество;
		Движение.Себестоимость = Выборка.Себестоимость;
	КонецЦикла;
	
	//Обязательно взводится флаг ...Записывать = Истина, без него движения записаны не будут. 
	Движения.ОстаткиНоменклатуры.Записывать = Истина;

КонецПроцедуры

